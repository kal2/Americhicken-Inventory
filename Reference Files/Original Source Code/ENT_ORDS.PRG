*************
* Program.: ENT_ORDS.PRG
* Author..: Grace Keller
* Date....: 10/26/87
* Notice..: Copyright 1987, Solid Software, Inc., All Rights Reserved
* Version.: FOXPRO
* Notes...: DAILY ORDERS ENTERED
*
* ---OPEN FILES
*  MULTI-USER RECORD/FILE LOCKING  +(M1)  FoxPro   Multi-User Code Insertion     
CLEAR
@ 00,00 SAY MCOMPANY
@ 01,71 SAY DATE()
@ 01,00 SAY MPROGRAM + ' - DAILY ORDERS ENTERED'                 
@ 2,0 to 24,79
@ 21,16 SAY SPACE(34)
@ 07,3 SAY 'SELECT FOLLOWING OPTION'
STORE DATE() TO MCUTOFF

IF DOW(MCUTOFF) = 1
   MCUTOFF = MCUTOFF - 3
ELSE
   MCUTOFF = MCUTOFF - 1
ENDIF

@ 10,3 SAY 'Date Orders Were Entered: ' GET MCUTOFF
READ

MPAGE_LEN = 56
MPRT_ACT = 'C'
DO PRT_SET
IF MPRT_ACT <> 'C'
   RETURN
ENDIF
MPAGE_NO = 0
MLINE = 99

SELECT B
DO FOX_USE WITH "&MDBFLOC.BUYER INDEX &MDBFLOC.BUY_CODE", .F.
@ 11,3 SAY 'NOW SELECTING, PRINTING........'
SET CONSOLE OFF
IF FILE("C:\COMPINFO\PRINTERS.DBF")
   SET PRINT TO C:\COMPINFO\PRT_FILE.TXT
ELSE
   SET PRINT TO LPT3
ENDIF
SET DEVICE TO PRINT
SET PRINT ON

*** PRINT IN 10 CPI
?? chr(27) + '&k0S'

*** LETTER GOTHIC AND BOLDED PRINT
?? chr(27) + '(s4102T' + chr(27) + '(s3B' + chr(27) + '(s3B'

*** LETTER SIZE PAPER
?? CHR(27) + '&l2A'

*** PORTRAIT MODE
?? CHR(27) + '&l0O'

SELECT D
DO FOX_USE WITH "PO_DTL INDEX PO_DTL", .F.

SELECT A
DO FOX_USE WITH "PO_HEAD", .F.
SET FILTER TO PO_DTADD = MCUTOFF
GOTO TOP

DO WHILE .NOT. EOF()
   
   IF BBUYCODE = 'INVN'
      SKIP
      LOOP
   ENDIF
   
   IF MLINE > MPAGE_LEN
      IF MLINE <> 99
         EJECT        
      ENDIF

      MPAGE_NO = MPAGE_NO + 1
      MLINE = 1
      @ MLINE,1  SAY 'PAGE NBR: ' + STR(MPAGE_NO,2)
      @ MLINE,20 SAY MCOMPANY + SPACE(20) + DTOC(DATE())
      MLINE = MLINE + 1
      @ MLINE,16 SAY 'ORDERS ENTERED ON: ' + DTOC(MCUTOFF)
      MLINE = MLINE + 2
      @ MLINE,1 SAY 'PO NUMBER'
      @ MLINE,11 SAY 'SHIP DATE'
      @ MLINE,23 SAY 'DELIVERY'
      @ MLINE,35 SAY 'SHIP TO CUSTOMER'
      @ MLINE,69 SAY 'PRICE/QTY'
      MLINE = 6
   ENDIF
   
   IF SALESMAN <> '  '
      @ MLINE,1 SAY SALESMAN
      @ MLINE,3 SAY '-'         
   ENDIF

   @ MLINE,4 SAY PO_NBR
   IF PO_SUFFIX <> ' '
      @ MLINE,8 SAY '-' + PO_SUFFIX
   ENDIF
   @ MLINE,11 SAY SHIP_DATE
   @ MLINE,23 SAY DEL_REQD
   SELECT B
   SEEK A->BUY_CODE
   IF EOF()
      @ MLINE,35 SAY 'CUSTOMER NOT ON FILE'
   ELSE
      @ MLINE,35 SAY NAME
   ENDIF

   MLINE = MLINE + 1

   SELECT D
   SEEK A->SALESMAN + A->PO_NBR + A->PO_SUFFIX

   MPRT_ADDR = .T.
   DO WHILE .NOT. EOF() .AND. SALESMAN = A->SALESMAN .AND. PO_NBR = A->PO_NBR .AND. PO_SUFFIX = A->PO_SUFFIX
      IF MLINE > MPAGE_LEN
         IF MLINE <> 99
            EJECT        
         ENDIF
         MPAGE_NO = MPAGE_NO + 1
         MLINE = 1
         @ MLINE,1  SAY 'PAGE NBR: ' + STR(MPAGE_NO,2)
         @ MLINE,20 SAY MCOMPANY + SPACE(20) + DTOC(DATE())
         MLINE = MLINE + 1
         @ MLINE,16 SAY 'ORDERS ENTERED ON: ' + DTOC(MCUTOFF)
         MLINE = MLINE + 2
         @ MLINE,1 SAY 'PO NUMBER'
         @ MLINE,11 SAY 'SHIP DATE'
         @ MLINE,23 SAY 'DELIVERY'
         @ MLINE,35 SAY 'SHIP TO CUSTOMER'
         @ MLINE,69 SAY 'PRICE/QTY'
         MLINE = 6
      ENDIF
      @ MLINE,11 SAY PROD_DESC
      @ MLINE,23 SAY QTY PICTURE '9999'

      IF MPRT_ADDR
         @ MLINE,35 SAY TRIM(B->CITY) + ', ' + B->STATE
         MPRT_ADDR = .F.
      ENDIF
      @ MLINE,70 SAY SEL_PRICE PICTURE '99.9999'
      MLINE = MLINE + 1
      SKIP
   ENDDO
   MLINE = MLINE + 2
   SELECT A
   SKIP
ENDDO

IF MLINE =  99
   @ 10,3 SAY 'NO ORDERS ENTERED ON: ' + DTOC(MCUTOFF)
   @ 11,3 SAY ' '
ELSE
   @ MLINE,1 SAY ' '
ENDIF

EJECT

SET DEVICE TO SCREEN
SET PRINT OFF
SET PRINT TO
SET CONSOLE ON
IF MPRINTER3 <> 'DONOTPRINT'
   RUN &MPRINTIT3
ENDIF


CLOSE DATA
MRPT_SEL   = ' '
RETURN

* EOF: ENT_ORDS.PRG
*Formatted by: ToolBox Ver. 1.2  on 3/26/21 at 9:38 AM.
