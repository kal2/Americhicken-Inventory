*************  
* Program.: REPRINT.PRG
* Author..: Grace Keller
* Date....: 10/26/87
* Notice..: Copyright 1987, Solid Software, Inc., All Rights Reserved
* Version.: FOXPRO
* Notes...: PRINT INVOICES SHEDULED
*
* ---OPEN FILES
*  MULTI-USER RECORD/FILE LOCKING  +(M1)  FoxPro   Multi-User Code Insertion     
MTITLE = ' - REPRINT INVOICE'
SELECT A
USE 
SELECT B
USE
SELECT C
USE
SELECT A
DO FOX_USE WITH "&MMASTDBF.CONTROL", .F.
STORE TRAN_MO TO MTRAN_MO
STORE TRAN_YR TO MTRAN_YR
DO FOX_USE WITH "COMPANY",.F.
STORE ANNOUNCE TO MANNOUNCE 
USE
STORE ' ACCOUNTS PAYABLE ' TO MPO_ATTN
STORE '????'      TO MPO_NBR

DO WHILE MPO_NBR <> SPACE(4)
   STORE SPACE(2)    TO MSALESMAN
   STORE SPACE(4)    TO MPO_NBR
   STORE SPACE(1)    TO MPO_SUFFIX
   MFORMS = 'YES'
   MPRT_MSG = 'YES'
   
   STORE 1 TO MACTION
   MPO_SEL = .T.
   DO WHILE MPO_SEL
      
      CLEAR 
      @  1, 0 SAY MCOMPANY
      @  1,68 SAY DATE()
      MTITLE = ' - SELECT PURCHASE ORDER TO REPRINT'
      @  2, 0 SAY MPROGRAM + MTITLE
      @ 3,0 TO 22,79
      @  5, 5 SAY 'Enter Search Field:'
      @  8, 5 SAY 'Purchase Order Number'
      @ 10, 5 GET Msalesman PICTURE '##'
      @ 10,7 say '-'
      @ 10,8 get mpo_nbr picture '####'
      @ 10,12 say '-'
      @ 10,13 get mpo_suffix picture '@!'
      **@ 13,5 SAY 'Are you using NEW invoice forms (YES/NO) ? ' GET MFORMS PICTURE '@!'
      @ 15,5 SAY 'Do you want to print "CORRECTED INVOICE"? ' GET MPRT_MSG PICTURE '@!'
      STORE 1 TO MACTION
      @ 24,01 SAY 'Action: ' + STR(MACTION,1)  
      @ 24,13 SAY '1. Continue    2. Edit'
      
      READ
      
      @24,09 GET MACTION PICTURE '9' RANGE 1,2
      READ               
      
      IF MACTION = 2
         LOOP
      ENDIF
      SELECT A
      DO FOX_USE WITH "PO_HEAD INDEX PO_HEAD", .F.
      IF MSALESMAN <> SPACE(2) .OR. MPO_NBR <> SPACE(4)
         SEEK MSALESMAN + MPO_NBR + MPO_SUFFIX
         IF EOF() .OR. DELETED()
            @ 23,0 CLEAR
            MWAIT = ' '
            @ 24,10 SAY 'PURCHASE ORDER NOT ON FILE - PRESS RETURN TO SELECT AGAIN ' GET MWAIT
            READ
            LOOP
         ENDIF
      ENDIF
      MPO_SEL = .F.
   ENDDO
   
   IF MPO_NBR = SPACE(4)
      LOOP
   ENDIF
   
   IF INV_NBR = SPACE(5)
      @ 23,0 CLEAR
      MWAIT = ' '
      @ 24,10 SAY 'INVOICE HAS NOT BEEN PRINT FIRST TIME - SELECT AGAIN ' GET MWAIT
      READ
      MPO_NBR = SPACE(4)
      LOOP
   ENDIF
   
   *** DONT PRINT IF NO CUSTOMER OR CUSTOMER IS INVENTORY
   IF BBUYCODE = 'NONE' .OR. BBUYCODE = 'INVN'
      MPO_NBR = SPACE(4)
      LOOP
   ENDIF
   
   IF SHIP_NBR <> SPACE(4)
      MPO_NBR = SPACE(4)
      LOOP
   ENDIF
   
   STORE INV_NBR TO MSEEK_INV 
   STORE VAL(INV_NBR) TO MINV_NBR
   STORE ' ' TO MACTION
   DO PRT_SET
   IF MACTION <> 'C'
      MPO_NBR = SPACE(4)
      LOOP
   ENDIF 
   
   SET CONSOLE OFF
   IF FILE("C:\COMPINFO\PRINTERS.DBF")
      SET PRINT TO C:\COMPINFO\PRT_FILE.TXT
   ELSE
      SET PRINT TO LPT3
   ENDIF
   SET DEVICE TO PRINT
   SET PRINT ON
   
   ** GOTHIC 12 BOLD
   ?? CHR(27) + '(s0p12h0s3b4102T'
   
   *** LETTER SIZE PAPER
   ?? CHR(27) + '&l2A'
   
   *** PORTRAIT MODE
   ?? CHR(27) + '&l0O'
   
   *** SELECT TRAY 2
   ?? CHR(27) + '&l1H'

   SELECT B
   DO FOX_USE WITH "PO_DTL INDEX PO_DTL", .F.
   STORE SPACE(4) TO MPROD
   MFIRST_TM = .T.
   MFIRST_LN = .T.
   MHEAD_PRT = .T.
   MPROD = SPACE(5)
   SELECT A
   STORE BUY_CODE TO MBUY_CODE
   STORE BBUYCODE TO MBBUYCODE
   STORE SUP_CODE TO MSUP_CODE
   SELECT C
   DO FOX_USE WITH "&MDBFLOC.BUYER INDEX &MDBFLOC.BUY_CODE", .F.
   SEEK MBUY_CODE
   IF .NOT. EOF()
      STORE NAME TO MSHP_NAME       
      STORE STREET TO MSHP_STRT  
      STORE CITY TO MSHP_CITY       
      STORE STATE TO MSHP_STATE
      IF ZIP4 = SPACE(4)
         STORE ZIP + SPACE(5) TO MSHP_ZIP
      ELSE
         STORE ZIP + '-' + ZIP4 TO MSHP_ZIP 
      ENDIF
   ELSE
      IF MBUY_CODE = 'NONE'
         STORE 'NONE' TO MSHP_NAME
      ELSE
         STORE 'SHIP TO NOT ON FILE'+ SPACE(13) TO MSHP_NAME
      ENDIF
      STORE SPACE(28) TO MSHP_STRT           
      STORE SPACE(20) TO MSHP_CITY            
      STORE SPACE(2) TO MSHP_STATE             
      STORE SPACE(10) TO MSHP_ZIP          
   ENDIF
   
   DO FOX_USE WITH "&MDBFLOC.SUPPLIER INDEX &MDBFLOC.SUP_CODE", .F.
   SEEK MSUP_CODE
   IF .NOT. EOF()
      STORE NAME TO MPRODUCER       
   ELSE
      STORE SPACE(32) TO MPRODUCER 
   ENDIF
   MCOD = .F.
   MCUST_SW = .F.
   DO FOX_USE WITH "&MDBFLOC.BIL_BUY INDEX &MDBFLOC.BBUYCODE", .F.
   SEEK MBBUYCODE
   IF .NOT. EOF()
      STORE NAME TO MBIL_NAME       
      IF SUBSTR(NOTE,1,3) = 'COD'
         MCOD = .T.
      ENDIF
      STORE REG_NAME TO MREG_NAME
      STORE 0 TO MTERM_DAYS
      IF ILINE1 = SPACE(3)
         STORE STREET + SPACE(9) TO MBIL_ADR1
         IF ZIP4 = SPACE(4)
            STORE ZIP + SPACE(5) TO MBIL_ZIP
         ELSE
            STORE ZIP + '-' + ZIP4 TO MBIL_ZIP 
         ENDIF
         STORE TRIM(CITY) + ', ' + STATE  + '  ' + MBIL_ZIP TO MBIL_ADR2
         STORE LEN(MBIL_ADR2) TO MLEN
         MLEN = 37 - MLEN
         MBIL_ADR2 = MBIL_ADR2 + SPACE(MLEN)
         MBIL_ADR3 = SPACE(37)
      ELSE
         STORE ILINE1 + SPACE(2) TO MBIL_ADR1
         STORE ILINE2 + SPACE(2) TO MBIL_ADR2
         STORE ILINE3 + SPACE(2) TO MBIL_ADR3
      ENDIF
      
      IF CUST_SW = 'Y'
         MCUST_SW = .T.
      ENDIF
   ELSE
      STORE 'BILL TO NOT ON FILE' + SPACE(13) TO MBIL_NAME
      STORE SPACE(32) TO MREG_NAME
      STORE SPACE(37) TO MBIL_ADR1
      STORE SPACE(37) TO MBIL_ADR2
      STORE SPACE(37) TO MBIL_ADR3
      STORE 0 TO MTERM_DAYS
   ENDIF
   
   SELECT A
   STORE SALESMAN TO MSALESMAN
   STORE FRT_CODE1 TO MFRT_CODE1
   STORE PO_NBR TO MPO_NBR
   STORE PO_SUFFIX TO MPO_SUFFIX
   STORE SAL_CODE2 TO MSAL_CODE2
   STORE SHIP_DATE TO MSHIP_DATE
   
   IF A->COM_CODE <> SPACE(2)
      SELECT C
      DO FOX_USE WITH "&MDBFLOC.COMPAYEE INDEX &MDBFLOC.COM_CODE", .F.
      SEEK A->COM_CODE   
      IF .NOT. EOF()
         STORE NAME TO MCOM_NAME       
      ELSE
         STORE 'UNKNOWN' + SPACE(25) TO MCOM_NAME
      ENDIF
   ELSE
      STORE SPACE(32) TO MCOM_NAME
   ENDIF
   
   SELECT C
   DO FOX_USE WITH "&MMASTDBF.SALESMAN INDEX &MMASTDBF.SAL_CODE", .F.
   IF A->BROK_NAME <> SPACE(5)
      IF A->BROK_NAME = 'ROBER'
         STORE "ROBERT RODRIGUEZ        " + SPACE(1) TO MSAL_NAME 
      ELSE
         STORE A->BROK_NAME + SPACE(20) TO MSAL_NAME
      ENDIF
   ELSE
      IF MSALESMAN <> SPACE(2)
         DO CASE
         CASE MCOMPANY = 'AMERICHICKEN, INC' .OR. MSALESMAN = '11'
            STORE SUBSTR(MCOM_NAME,1,25) TO MSAL_NAME
         OTHERWISE
            SEEK MSALESMAN
            IF .NOT. EOF()
               STORE NAME TO MSAL_NAME
            ELSE
               STORE 'BROKER NAME NOT FOUND'+ SPACE(3) TO MSAL_NAME
            ENDIF
         ENDCASE
      ENDIF
   ENDIF
   
   SELECT C
   DO FOX_USE WITH "&MDBFLOC.FREIGHT INDEX &MDBFLOC.FRT_CODE", .F.
   IF MFRT_CODE1 = 'CUST'
      STORE 'CUSTOMER PICK UP' TO MFRT_NAME
   ELSE 
      IF MFRT_CODE1 = 'NONE'
         IF MCOMPANY = 'DIVISION 15'
            STORE 'WILL CALL' TO MFRT_NAME
         ELSE
            STORE 'DELIVERED' TO MFRT_NAME
         ENDIF
      ELSE
         SEEK MFRT_CODE1                
         IF .NOT. EOF()
            STORE NAME TO MFRT_NAME
         ELSE
            STORE SPACE(10) TO MFRT_NAME
         ENDIF
      ENDIF
   ENDIF
   DO FOX_USE WITH "PRT_NOTE INDEX PRT_NOTE", .F.
   SEEK MSALESMAN + MPO_NBR + MPO_SUFFIX
   IF .NOT. EOF()
      STORE INV_COMMT TO MINV_COMMT
   ELSE
      STORE SPACE(60) TO MINV_COMMT     
   ENDIF
   
   ******** WRITE INVOICE RECORD 
   SELECT C
   DO FOX_USE WITH "INV_HEAD INDEX INV_HEAD, INV_DATE", .F.
   
   SEEK MSEEK_INV
   DO FILELOCK
   IF EOF()
      APPEND BLANK
      REPLACE inv_date   WITH DATE()       
      REPLACE inv_nbr    WITH STR(minv_nbr,5)   
   ENDIF
   
   REPLACE salesman   WITH A->salesman  
   REPLACE po_nbr     WITH A->po_nbr    
   REPLACE po_suffix  WITH A->po_suffix 
   REPLACE SHIP_DATE  WITH A->SHIP_DATE
   REPLACE TOT_SELL   WITH A->TOT_SELL + A->CUS_PALL
   UNLOCK
   
   SELECT E
   DO FOX_USE WITH "CUS_INV INDEX CUSIVNBR, CUS_INV, CUS_DATE, CUS_SHIP", .F.
   SEEK MSEEK_INV 
   DO FILELOCK
   IF EOF()
      APPEND BLANK
      REPLACE inv_date   WITH DATE()       
   ENDIF
   
   REPLACE salesman   WITH A->salesman  
   REPLACE po_nbr     WITH A->po_nbr    
   REPLACE po_suffix  WITH A->po_suffix 
   REPLACE buy_code   WITH A->buy_code  
   REPLACE bbuycode   WITH A->bbuycode  
   REPLACE ship_date  WITH A->ship_date 
   REPLACE tot_lbs    WITH A->tot_lbs   
   REPLACE tot_sell   WITH A->tot_sell  
   REPLACE inv_nbr    WITH STR(minv_nbr,5)   
   REPLACE BROKERage  WITH A->BROKERage 
   REPLACE CUS_PALL   WITH A->CUS_PALL
   
   UNLOCK
   
   DO FOX_USE WITH "INV_ADJ INDEX INV_ADJ", .F.
   SEEK STR(MINV_NBR,5)
   
   DO WHILE .NOT. EOF() .AND. INV_NBR = STR(MINV_NBR,5)
      DELETE
      SKIP
   ENDDO
   STORE SUBSTR(DTOC(A->SHIP_DATE),7) TO MYR
   STORE SUBSTR(DTOC(A->SHIP_DATE),1,2) TO MMO
   STORE MYR+MMO TO MDATE
   STORE VAL(MDATE) TO MDATE  
   
   STORE MTRAN_YR+MTRAN_MO TO MTRAN_DATE
   STORE VAL(MTRAN_DATE) TO MTRAN_DATE
   
   IF MDATE > MTRAN_DATE 
      STORE SUBSTR(STR(MONTH(A->SHIP_DATE) + 100,3),2) TO MTRAN_MO
      STORE SUBSTR(DTOC(A->SHIP_DATE),7) TO MTRAN_YR
   ENDIF
   
   DO FOX_USE WITH "DAILYJRN INDEX JRN_PO, JRN_MO, JRN_TRAN, POSTED", .F.
   SEEK  A->SALESMAN + A->PO_NBR + A->PO_SUFFIX  + ' I'
   IF .NOT. EOF() .AND. TRAN_YR <> MTRAN_YR .AND. TRAN_MO <> MTRAN_MO
      DO FILELOCK
      REPLACE TRAN_MO    WITH MTRAN_MO
      REPLACE TRAN_YR    WITH MTRAN_YR
      REPLACE JRNL_DATE  WITH A->ship_date
      UNLOCK
   ENDIF
   USE
   
   *** LASER PRINTS - DO TWICE

   MCK_BROK = .T.
   MTOTAL = 0
   MPRT_ONE = .F.
   MNO_ITM = .T.
   *** THIS IS USED TO KNOW WHETHER TO PRINT S/W'S ON INVOICE
   *** USED TO BE COPY_ONLY = .F.
   MCOPY_ONLY = .T.
   MLINE = 1
   ***********
   DO INV_DOC

   SELECT B
   USE
   SELECT C
   USE
   SELECT E
   USE

   MCK_BROK = .T.
   MTOTAL = 0
   MPRT_ONE = .F.
   MNO_ITM = .T.
   *** THIS IS USED TO KNOW WHETHER TO PRINT S/W'S ON INVOICE
   *** USED TO BE COPY_ONLY = .F.
   MCOPY_ONLY = .T.
   MLINE = 1
   DO INV_DOC

   SELECT B
   USE
   SELECT C
   USE
   SELECT E
   USE
   ***********

   SELECT B
   DO FOX_USE WITH "PO_DTL INDEX PO_DTL", .F.
   SEEK MSALESMAN + MPO_NBR + MPO_SUFFIX
   IF EOF()
      MNO_ITM = .T.
   ELSE
      MNO_ITM = .F.
   ENDIF
   SELECT A
   IF MNO_ITM
      DO RECLOCK
      REPLACE SHORT_FLG WITH 'Y'
      UNLOCK
   ENDIF

   IF MPRT_ONE
      EJECT
   ENDIF
   
   SET DEVICE TO SCREEN
   SET PRINT OFF
   SET PRINT TO
   SET CONSOLE ON
   IF MPRINTER1 <> 'DONOTPRINT'
      RUN &MPRINTIT1
   ENDIF
   
   MPO_NBR = '????'
   
ENDDO
CLOSE ALL
RETURN
* EOF: REPRINT.PRG
*Formatted by: ToolBox Ver. 1.2  on 1/7/16 at 3:12 PM.
